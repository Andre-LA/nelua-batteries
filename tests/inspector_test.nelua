require 'inspector'
require 'nester'

nester.describe("inspector", function()
  nester.describe("primitives", function()
    nester.describe("simple", function()
      nester.it("booleans", function()
        local result <close> = inspector(true)
        expect.equal(result, "true")

        local result <close> = inspector(false)
        expect.equal(result, "false")
      end)

      nester.it("integers", function()
        local result <close> = inspector(10)
        expect.equal(result, "10_i64")
      end)

      nester.it("numbers", function()
        local result <close> = inspector(20.20)
        expect.equal(result, "20.2_f64")

        local result <close> = inspector(30_number)
        expect.equal(result, "30.0_f64")
      end)

      nester.it("strings", function()
        local result <close> = inspector("hello world!")
        expect.equal(result, '"hello world!"')

        local result <close> = inspector("hello world!"_cstring)
        expect.equal(result, '"hello world!" --[[ @cstring ]]')
      end)
    end)

    nester.it("enums", function()
      local Weeks = @enum{
        Sunday = 0,
        Monday,
        Tuesday,
        Wednesday,
        Thursday,
        Friday,
        Saturday
      }

      local ByteWeeks = @enum(byte){
        Sunday = 0,
        Monday,
        Tuesday,
        Wednesday,
        Thursday,
        Friday,
        Saturday
      }

      local result <close> = inspector(Weeks.Tuesday)
      expect.equal(result, "Weeks.Tuesday --[[ = 2_i64 ]]")

      local result <close> = inspector(ByteWeeks.Friday)
      expect.equal(result, "ByteWeeks.Friday --[[ = 5_u8 ]]")
    end)

    nester.describe("arrays", function()
      nester.it("boolean arrays", function()
        local result <close> = inspector((@array(boolean, 3)){true, false, false})
        expect.equal(result, "(@array(boolean, 3)){ true, false, false }")

        -- using syntax sugar
        local result <close> = inspector((@[2]boolean){false, true})
        expect.equal(result, "(@array(boolean, 2)){ false, true }")
      end)

      nester.it("scalar arrays", function()
        local result <close> = inspector((@array(uint16, 3)){3, 4, 5})
        expect.equal(result, "(@array(uint16, 3)){ 3, 4, 5 }")

        -- using syntax sugar
        local result <close> = inspector((@[2]integer){1, 2})
        expect.equal(result, "(@array(int64, 2)){ 1, 2 }")

        -- inferred size
        local result <close> = inspector((@[]number){-3, -2})
        expect.equal(result, "(@array(float64, 2)){ -3.0, -2.0 }")
      end)
    end)

    nester.describe("records", function()
      nester.it("simple", function()
        local SimpleRec = @record{
          a: integer,
          b: number
        }

        local result <close> = inspector((@SimpleRec){ 10, 20.20 })
        expect.equal(result, #[
          table.concat({
            '(@SimpleRec){',
            '  a = 10_i64,',
            '  b = 20.2_f64,' ,
            '}'
          }, '\n')
        ]#)

        local SimpleArrRec = @record{
          a: [3]integer,
        }

        local result <close> = inspector((@SimpleArrRec){ a = { 1, 2, 3 } })
        expect.equal(result, #[
          table.concat({
            '(@SimpleArrRec){',
            '  a = (@array(int64, 3)){ 1, 2, 3 },',
            '}'
          }, '\n')
        ]#)
      end)

      nester.it("nested records", function()
        local InnerRecord = @record{
          a: integer,
        }

        local OuterRecord = @record{
          ir: InnerRecord,
          b: [2]integer
        }

        local result <close> = inspector((@OuterRecord){ ir = { a = 10 }, b = {20, 30} })
        expect.equal(result, #[
          table.concat({
            '(@OuterRecord){',
            '  ir = (@InnerRecord){',
            '    a = 10_i64,',
            '  },',
            '  b = (@array(int64, 2)){ 20, 30 },',
            '}'
          }, '\n')
        ]#)
      end)

      nester.it("record arrays", function()
        local MyRec = @record{
          b: byte,
          i: int16
        }

        local result <close> = inspector((@[2]MyRec){
          { b = 1, i = 10},
          { b = 2, i = 20},
        })

        expect.equal(result, #[
          table.concat({
            '(@array(MyRec, 2)){ {',
            '  b = 1_u8,',
            '  i = 10_i16,',
            '}, {',
            '  b = 2_u8,',
            '  i = 20_i16,',
            '} }',
          }, '\n')
        ]#)
      end)
    end)
  end)
end)
